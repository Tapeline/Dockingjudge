# Cross-platform shell configuration
# Use PowerShell on Windows (higher precedence than shell setting)
set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
# Use sh on Unix-like systems
set shell := ["sh", "-c"]


gen-oas $what="all":
  just __gen_oas_{{what}}

__gen_oas_all:
  just __gen_oas_account
  just __gen_oas_contest
  just __gen_oas_solution
  just __gen_oas_judgelet

[working-directory: "../AccountService"]
__gen_oas_account:
  uv run python manage.py spectacular --file ../docs/docs/openapi/account.yml

[working-directory: "../ContestService"]
__gen_oas_contest:
  uv run python manage.py spectacular --file ../docs/docs/openapi/contest.yml

[working-directory: "../SolutionService"]
__gen_oas_solution:
  uv run litestar schema openapi --output ../docs/docs/openapi/solution.json 

[working-directory: "../JudgeUnit"]
__gen_oas_judgelet:
  uv run litestar schema openapi --output ../docs/docs/openapi/judgelet.json
